/*
 * Copyright 2012-2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'distribution'

group = componentGroup

// BUILD_NUMBER
// org.gradle.project.BUILD_NUMBER=systemPropertyValue
ext.env = System.getenv()
ext.buildNumber = (env.RELEASE_VERSION ?: componentVersion)
version = ext.buildNumber

targetCompatibility = componentJavaVersion
sourceCompatibility = componentJavaVersion
archivesBaseName = componentName

repositories {
	mavenLocal()
	mavenCentral()
}

configurations {
	provided
	providedCompile
}

sourceSets.main.compileClasspath += configurations.provided

dependencies {
	implementation("com.google.code.gson:gson:${gsonVersion}")
	implementation("commons-io:commons-io:${commonsIoVersion}")
	implementation("org.apache.commons:commons-lang3:${commonsLangVersion}")

	testImplementation("junit:junit:${junitVersion}")
}

jar {
	// fixed jar name to simplify batch file
	archiveName = "${componentName}.jar"

	manifest {
		attributes "Implementation-Title": archivesBaseName,
		           "Implementation-Version": version,
		           "Main-Class": componentMainClass
	}

	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}

	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

compileJava.dependsOn(processResources)

tasks.withType(JavaCompile) {
	options.fork = true
	options.incremental = true
	options.compilerArgs += ["-parameters"]
}

artifacts {
	archives jar
}

task distro {
	group "Build"
	description "build project clean room and update lib/ directory"
	mustRunAfter clean
	dependsOn build
}

distributions {

	file("$buildDir/distributions").mkdirs()
	file("$buildDir/distributions/version.txt").text = version

	main {
		contents {
			into('lib') {
				from jar
			}
			into('bin') {
				from 'bin'
			}
			into('/') {
				from "$buildDir/distributions/version.txt"
			}
			into '/'
		}
	}
}
